// build.gradle (root project)
// Add pluginManagement block to specify where to find plugins
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4' apply false
    id 'io.spring.dependency-management' version '1.1.6'
}

allprojects {
    group = 'com.capitole.challenge.starwars'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.4"
            mavenBom "org.zalando:logbook-bom:3.12.2"
        }
        dependencies {
            dependency "io.rest-assured:rest-assured:5.4.0"
            dependency "org.mapstruct:mapstruct:1.5.5.Final"
            dependency "org.jeasy:easy-random-core:5.0.0"
            dependency "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.4.0"
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'

        implementation 'org.mapstruct:mapstruct:1.5.5.Final'
        implementation 'org.springframework.boot:spring-boot-starter-logging'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
        annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += [
                "-parameters"
        ]
    }

    tasks.withType(org.springframework.boot.gradle.tasks.bundling.BootJar) {
        enabled = false
    }
}